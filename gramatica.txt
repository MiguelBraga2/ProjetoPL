
lines : lines line
      | line

line : tagline
     | JSCODE
     | comment
     | conditional
     | iteration
     | case

case : CASE JSCODE INDENT lines DEDENT
     | WHEN JSCODE INDENT lines DEDENT
     | DEFAULT INDENT lines DEDENT    

comment : COMMENT comment_text
        | COMMENT

comment_text : comment_text TEXT
             | TEXT   

conditional : IF CONDITION INDENT lines DEDENT
            | ELSE IF CONDITION INDENT lines DEDENT 
            | ELSE INDENT lines DEDENT
            | UNLESS CONDITION INDENT lines DEDENT
            | IF CONDITION
            | ELSE IF CONDITION 
            | ELSE
            | UNLESS CONDITION

iteration : EACH IDENTIFIER IN JSCODE INDENT lines DEDENT

tagline : tag content INDENT lines DEDENT 
        | tag INDENT lines DEDENT 
        | tag content 
        | tag BAR
        | tag DOT text
        | tag

tag : TAG attributes 
    | TAG
    | class_id_attributes LPAREN pug_attributes RPAREN 
    | class_id_attributes

attributes : class_id_attributes LPAREN pug_attributes RPAREN
           | class_id_attributes
           | LPAREN pug_attributes RPAREN

class_id_attributes : classes ID CLASS
                    | ID classes
                    | classes ID
                    | classes
                    | ID

classes : classes CLASS
        | CLASS

pug_attributes : pug_attributes COMMA pug_attribute
               | pug_attributes pug_attribute
               | pug_attribute

pug_attribute : ATTRIBUTENAME EQUALS attribute_value

attribute_value : STRING
                | BOOLEAN
                | NUMBER


content : EQUALS interpolation
        | text

interpolation : VARIABLE
              | STRING 
              | NUMBER

text : text BEGININTERP interpolation ENDINTERP
     | text TEXT
     | BEGININTERP interpolation ENDINTERP
     | TEXT
