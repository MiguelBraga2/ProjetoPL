
lines : lines line
      | line

line : tagline
     | JSCODE
     | comment
     | conditional
     | iteration
     | switch

switch : CASE CONDITION INDENT casesdefault DEDENT

casesdefault : cases DEFAULT INDENT lines DEDENT
             | cases
             | DEFAULT INDENT lines DEDENT

cases : cases case 
      | case  

case : WHEN CONDITION INDENT lines DEDENT

comment : COMMENT comment_text
        | COMMENT

comment_text : comment_text TEXT
             | TEXT   

conditional : conditional_begin conditional_middle conditional_final
            | conditional_begin conditional_final
            | conditional_begin conditional_middle
            | conditional_begin

conditional_begin : IF CONDITION INDENT lines DEDENT
                  | IF CONDITION
                  | UNLESS CONDITION INDENT lines DEDENT 
                  | UNLESS CONDITION

conditional_middle : conditional_middle ELSE IF INDENT lines DEDENT 
                   | conditional_middle ELSE IF 
                   | ELSE IF INDENT lines DEDENT 
                   | ELSE IF 

conditional_final : ELSE INDENT lines DEDENT
                  | ELSE 


iteration : EACH IDENTIFIER IN JSCODE INDENT lines DEDENT
          | EACH IDENTIFIER COMMA IDENTIFIER IN JSCODE INDENT lines DEDENT

tagline : tag content INDENT lines DEDENT 
        | tag INDENT lines DEDENT 
        | tag content 
        | tag BAR
        | tag DOT text
        | tag

tag : TAG attributes 
    | TAG
    | class_id_attributes LPAREN pug_attributes RPAREN 
    | class_id_attributes

attributes : class_id_attributes LPAREN pug_attributes RPAREN
           | class_id_attributes
           | LPAREN pug_attributes RPAREN

class_id_attributes : classes ID CLASS
                    | ID classes
                    | classes ID
                    | classes
                    | ID

classes : classes CLASS
        | CLASS

pug_attributes : pug_attributes COMMA pug_attribute
               | pug_attributes pug_attribute
               | pug_attribute

pug_attribute : ATTRIBUTENAME EQUALS attribute_value

attribute_value : STRING
                | BOOLEAN
                | NUMBER


content : EQUALS interpolation
        | text

interpolation : VARIABLE
              | STRING 
              | NUMBER

text : text BEGININTERP interpolation ENDINTERP
     | text TEXT
     | BEGININTERP interpolation ENDINTERP
     | TEXT
